      PROGRAM ISTATE
      IMPLICIT NONE
      INTEGER STRLEN
      PARAMETER (STRLEN=32)
      CHARACTER*(STRLEN) UTC0
      CHARACTER*(STRLEN) ABCORR
      CHARACTER*(STRLEN) FRAME
      INTEGER NBODY
      PARAMETER(NBODY=5)
      INTEGER NMASS
      PARAMETER(NMASS=5)
      CHARACTER*7 MASSES(NMASS)
      CHARACTER*18 BODIES(NBODY)
      DOUBLE PRECISION GM,GMSUN,STATE(6),ET0,JD0,LT
      INTEGER I,N
      DATA MASSES/'SUN','VENUS','EARTH','JUPITER','SATURN'/
      DATA BODIES/'VENUS BARYCENTER','EARTH BARYCENTER',
     *  'JUPITER BARYCENTER','SATURN BARYCENTER','CASSINI'/
C
      CALL FURNSH('cassini.meta')
C
      CALL PROMPT('Enter the beginning UTC time: ', UTC0)
      CALL STR2ET(UTC0, ET0)
C
      OPEN(UNIT=8,FILE='initialstate.out',STATUS='UNKNOWN')
C
      JD0=2451545.0D0+ET0/86400.0D0
      WRITE(8,1000) JD0
 1000 FORMAT(F15.6)
C
      DO I=1,NMASS
        CALL BODVRD(MASSES(I) , 'GM', 1, N, GM )
        WRITE(8,1001) GM
        IF (I.EQ.1) THEN
          WRITE(8,1003) NMASS-1
        END IF
      END DO
 1001 FORMAT(D20.12)
 1003 FORMAT(I2)
C
      ABCORR='NONE'
      FRAME='J2000'
C
      WRITE(8,1003) NBODY
C
      DO I=1,NBODY
        CALL SPKEZR(BODIES(I), ET0, FRAME, ABCORR, 'SUN', STATE, LT)
        WRITE(8,1002) STATE
      END DO
 1002 FORMAT(D20.12,5(1X,D20.12))
C
      CLOSE(UNIT=8,STATUS='KEEP')
C
      CALL BYEBYE( 'SUCCESS' )
      END
